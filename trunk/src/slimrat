#!/usr/bin/env perl
#
# slimrat - main CLI script
#
# Copyright (c) 2008-2009 Přemek Vyhnal
# Copyright (c) 2009 Tim Besard
#
# This file is part of slimrat, an open-source Perl scripted
# command line and GUI utility for downloading files from
# several download providers.
# 
# Permission is hereby granted, free of charge, to any person
# obtaining a copy of this software and associated documentation
# files (the "Software"), to deal in the Software without
# restriction, including without limitation the rights to use,
# copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following
# conditions:
# 
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
# OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
# HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
# WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
# OTHER DEALINGS IN THE SOFTWARE.
#
# Authors:
#    Přemek Vyhnal <premysl.vyhnal gmail com>
#    Tim Besard <tim-dot-besard-at-gmail-dot-com>
#

#################
# CONFIGURATION #
#################

#
# Dependancies
#

# Packages
use Getopt::Long;
use Pod::Usage;

# Find root for custom packages
use FindBin qw($RealBin);
use lib $RealBin;

# Custom packages
use Common;
use Plugin;
use Toolbox;
use Queue;
use Log;
use Configuration;

# Function prototypes
sub quit($);
sub download($$$$);

# Write nicely
use strict;
use warnings;


#
# Essential stuff
#

# Register signals
$SIG{'INT'} = 'quit';

# Global variables
my (@failedlinks, @oklinks);


#
# Command-line options
#

# Process command-line options
Getopt::Long::Configure("pass_through");
Getopt::Long::Configure("bundling");

my %options;
GetOptions (
		\%options,
		"help|h|?",
		"man",
		"check|c",
		"list|l=s",
		"to|t=s",
		"address=s",
		"daemon|d",
		"kill|k",
		"debug",
		"quiet|q",
		"config=s",
);

# Initialise configuration
config_readfiles($options{"config"});
&config_cli->set_default("mode", "download");
&config_cli->set_default("to", ".");
&config_cli->set_default("image_viewer", "asciiview -kbddriver stdin -driver stdout %s");

# Give the usage or manual
if ($options{"man"}) {
	pod2usage(-verbose => 2);
	quit(0);
} elsif ($options{"help"}) {
	pod2usage(-verbose => 1);
	quit(0);
}

# Kill an instance if requested
if ($options{"kill"}) {
	if (my $pid = pid_read()) {
		if (kill 0, $pid) {
			info("Killing an active instance at PID $pid");
			kill 2, $pid;
			quit(0);
		} else {
			warning("no running instance found");
			quit(1); # ?
		}
	} else {
		fatal("could not read state file");
		quit(255);
	}
}

# Mode (e.g. what slimrat should do)
&config_cli->set("mode", "check") if ($options{"check"});

# Options we might use later on
&config_cli->set("list", $options{"list"}) if ($options{"list"});
&config_cli->set("daemon", 1) if ($options{"daemon"});
&config_cli->set("address", $options{"address"}) if ($options{"address"});
&config_cli->set("to", $options{"to"}) if ($options{"to"});
usage("cannot combine --debug with --quiet option") if ($options{"quiet"} && $options{"debug"});
config_verbosity(2) if ($options{"quiet"});
config_verbosity(5) if ($options{"debug"});


#
# Apply configuration
#

# Configure other packages and stuff
config_other();

# Check if we got input files (if certain mode does not require those, prepend those checks _before_ this conditional)
if (!scalar @ARGV && !&config_cli->get("list")) {
	usage("no input URLs");
}

# Initialise a link queue
my $queue = new Queue();
while (my $link = shift) {
	if ($link =~ /^\w+:\/\//) {
		$queue->add($link);
	} else {
		usage("unrecognised option \"$link\"");
	}
}
$queue->file(&config_cli->get("list")) if (&config_cli->get("list"));

# Set socket local address if needed
if(&config_cli->get("address")){
	no warnings;
	push(@LWP::Protocol::http::EXTRA_SOCK_OPTS, LocalAddr => $options{"address"});
}

# Fork in background
if (&config_cli->get("daemon")) {
	info("Forking in background");
	daemonize();
	print "\n\n";
}



########
# MAIN #
########

info("Slimrat started");

#
# Check
#

if (&config_cli->get("mode") eq "check") {
	info("Checking URLs");
	my $dead_links = 0;
	
	# Get and loop all URLs
 	while (my $link = $queue->get()) {
		my $alive = 0;
		# Load plugin
		my $plugin;
		unless ($plugin = Plugin->new($link, $mech)) {
			status($link, 0, "plugin failure");
		} else {
			my $status = $plugin->check();
			if ($status < 0) {
				status($link, $status, "file is dead");
			} elsif ($status == 0) {
				status($link, $status, "plugin failure (could not distinguish whether file is up or down)");
			} elsif ($status > 0) {
				my $size = $plugin->get_filesize();
				my $extra = ($plugin->get_filename()||"unknown filename") . ", " .  ($size ? bytes_readable($size) : "unknown filesize") if ($status>0);
				status($link, $status, $extra);
				$alive++
			}
		}
		$dead_links++ unless ($alive);
		
		# Advance to the next URL
		$queue->advance();
	}
	
	quit($dead_links);
}


#
# Download
#

elsif (&config_cli->get("mode") eq "download") {
 	while (my $link = $queue->get()) {
 		
 		# Download the URL with custom progress indication
 		my $failure = 0;
		my $result = download(
				$link,
				&config_cli->get("to"),
				sub { # Progress indication
					my ($done, $total, $last_done, $last_time) = @_; # Bytes downloaded, Filesize in bytes, Bytes downloaded from last run of this function, Time from last run
					my $speed = $last_done/$last_time if ($last_time); # current speed [B/s] TODO: maybe some average speed?
					if ($total) {
						my $perc = $done / $total;
						push my @progr, "Downloaded: ", int($perc*10000)/100, "%";
						push @progr, " -- speed: ", bytes_readable($speed), "/s -- ", seconds_readable(($total-$done)/$speed), " remaining" if ($speed);
						progress(@progr);
					} else {
						push my @progr, "Downloaded ", bytes_readable($done);
						push @progr, " -- speed: ", bytes_readable($speed), "/s" if ($speed);
						progress(@progr);
					}
				},
				sub { # Captcha handler
					my $captchafile = shift;
					
					# view
					system(sprintf &config_cli->get("image_viewer"), "/tmp/slimrat-captcha.tmp");
					
					# Ask the user
					print "Captcha? ";
					my $captcha = <>;
					chomp $captcha;
					return $captcha;
				}
		);
		
		# Check result
		if ($result > 0) {
			push @oklinks, $link;
			$queue->file_update($link, "DONE");

			# Command after successful download
			if (my $command = &config_cli->get("post_download")) {
				system($command);
			}
		}
		elsif ($result == 0) {
			$failure = 1;
		}
		elsif ($result == -1) {
			$failure = 1;
			$queue->file_update($link, "DEAD");
		}
		
		# Failure?
		if ($failure) {
			push @failedlinks, $link;

			# Command after failed download
			if (my $command = &config_cli->get("post_failure")) {
				system($command);
			}
		}
		
		# Advance to the next URL
		$queue->advance();
	}

	# Command after all downloads
	if (my $command = &config_cli->get("post_all")) {
		system($command);
	}

	quit(scalar @failedlinks);
}


#
# Other
#

else {
	usage("unrecognised mode");
	quit(255);
}



############
# ROUTINES #
############

# Finish
sub quit($) {
	# Print a download summary
	summary(\@oklinks, \@failedlinks);
	
	# Write the dump
	dump_write();
	
	info("Slimrat finished");

	my $return = shift;
	$return = 255 if ($return !~ /^\d+$/);
	# Hard exit, for when &quit gets called out of sig{int}	
	exit($return);
}

__END__

# Print the usage

=head1 NAME

slimrat-cli

=head1 VERSION

1.0.0-trunk

=head1 DESCRIPTION

  Command-line download manager, capable of downloading files from
  several free download providers

=head1 SYNOPSIS

  slimrat [OPTION...] [LINK]...

=head1 OPTIONS

=over 8

=item B<--help>

  Prints a summary how to use the client.

=item B<--man>

  Prints a manual how to use the client.

=item B<--daemon>

  Makes slimrat work in the background, by properly forking and redirecting
  the output to a specified logfile. Only one file can be backgrounded at a
  time, to support multiple instances you'll need to specify differend
  state files to save the instances PID in.

=item B<--kill>

  Kills a single active client, by looking up the PID in a predefined state file.

=item B<--list>

  Uses the given file as a queue-file containing URLs.

=item B<--check>

  Do not download the loaded URLs, just check them.

=item B<--to>

  Specifies the target directory for the downloaded files.

=item B<--address>

  Makes the download client bind to a specific address.

=item B<--config>

  Load custom configuration file.

=item B<--debug>

  Enables maximal verbosity, which includes a lot of text on the screen and the generation
  of an additional dump archive.

=item B<--quiet>

  Makes slimrat less verbose, only displaying errors and warnings.

=back

=head1 EXAMPLES

  slimrat http://rapidshare.com/files/012345678/somefile.xxx
  slimrat -l urls.dat -d

=cut

